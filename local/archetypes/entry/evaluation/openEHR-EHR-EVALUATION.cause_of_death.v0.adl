archetype (adl_version=1.4; uid=ce91d191-2ab5-478e-a349-6c4ce8b0cbea)
	openEHR-EHR-EVALUATION.cause_of_death.v0

concept
	[at0000]	-- Cause of death
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Mikkel Johan Gaup Grønmo">
				["organisation"] = <"Regional forvaltning EPJ, Helse Nord">
				["email"] = <"mikkel.johan.gaup.gronmo@helse-nord.no">
			>
			accreditation = <"Nasjonal IKT HF">
		>
	>
description
	original_author = <
		["name"] = <"Mikkel Johan Gaup Grønmo">
		["organisation"] = <"Forvaltningssenter EPJ, Helse-Nord RHF">
		["email"] = <"mikkel.johan.gaup.gronmo@helse-nord.no">
		["date"] = <"2020-08-03">
	>
	details = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere detaljer om spesifikk tilstand eller skader som forårsaket eller medvirket til individets død.">
			use = <"Brukes for å registrere detaljer om spesifik tilstand eller skader som forårsaket eller medvirket til individets død. Denne arketypen er laget for å brukes inne i konteksten av et Dødsattest templat. Den kan også benyttes i et templat der obduksjonsfunn blir registrert.

Eksempel på dødsårsak:
Hjerneblødning (Umiddelbar dødsårsak, A) 
- som følge av Metastase i hjernen (B)
- som følge av Brystkreft (C).">
			keywords = <"sertifikat", "årsak", "grunn">
			misuse = <"Skal ikke brukes for å registrere detaljer om tilstander eller skader som ikke har bidratt til individets død.
Bruk EVALUATION.problem_diagnosis eller liknende arketyper til det formålet.">
			copyright = <"© openEHR Foundation">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record details about specific conditions or injuries that caused or contributed to the death of an individual.">
			use = <"Use to record details about specific conditions or injuries that caused or contributed to the death of an individual. This archetype has been designed to be used within the context of a Death Certificate template. It may also be used within a template recording the findings from an autopsy.

An example for cause of death:
Cerebral haemorrhage (Direct cause, A) 
- due to Metastasis of the brain (B)
- due to Breast cancer (C).">
			keywords = <"certificate", "cause", "reason">
			misuse = <"Not to be used record details about conditions or injuries that have not contributed to the death of an individual.
Use the EVALUATION.problem_diagnosis or similar archetypes for this purpose.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Vebjørn Arntzen, Oslo University Hospital, Norway (openEHR Editor)", "Silje Ljosland Bakke, Helse Vest IKT AS, Norway (openEHR Editor)", "SB Bhattacharyya, Bhattacharyyas Clinical Records Research & Informatics LLP, India", "Yexuan Cheng, 浙江大学, China", "Candice de Lisser, Ministry of Health and Wellness, Jamaica", "Stefan Dubois, Dep't of Pathology and Genetics, University Hospitals of Lund and Malmö, Sweden", "Grant Forrest, Lunaria Ltd, United Kingdom", "Rosane Gotardo, Systema Ltda., Brazil", "Heather Grain, Llewelyn Grain Informatics, Australia", "Martin Grundberg, Cambio Healthcare Systems, Sweden", "Mikkel Johan Gaup Grønmo, Regional forvaltning EPJ, Helse Nord, Norway (openEHR Editor)", "Joost Holslag, Nedap, Netherlands", "Evelyn Hovenga, EJSH Consulting, Australia", "Kanika Kuwelker, Helse Vest IKT, Norway (openEHR Editor)", "Jörgen Kuylenstierna, eWeave AB, Sweden", "Liv Laugen, ​Oslo University Hospital, Norway, Norway (openEHR Editor)", "Heather Leslie, Atomica Informatics, Australia (openEHR Editor)", "Michael Lutz, BITsoft, Germany", "Manisha Mantri, C-DAC, India", "James McClay, University of Nebraska Medical Center, United States", "Paul Miller, NES Digital Service, NHS Scotland, United Kingdom", "Mikael Nyström, Cambio Healthcare Systems AB, Sweden", "Alan Smith, OXLEAS NHS Mental Health Foundation Trust, United Kingdom", "Natalia Strauch, Medizinische Hochschule Hannover, Germany", "Norwegian Review Summary, Norwegian Public Hospitals, Norway", "Anders Thurin, VGR, Sweden", "John Tore Valand, Helse Bergen, Norway (openEHR Editor)", "Martijn van Eenennaam, Nedap Healthcare, Netherlands", "Marit Alice Venheim, Helse Vest IKT, Norway (openEHR Editor)", "Lin Zhang, Taikang Insurance Group, China">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Cause of Death and the Death Certificate [Internet]. College of American Pathologists (CAP); 2006 [cited 2022 Jun 05]. Available from: https://www.health.state.mn.us/people/vitalrecords/physician-me/docs/capcodbook.pdf

International statistical classification of diseases and related health problems [Internet]. World Health Organisation; 2011, pp 211 & 214 [cited 2022 Jun 05]. Available from: https://icd.who.int/browse10/Content/statichtml/ICD10Volume2_en_2016.pdf.

Physicians' Manual on Medical Certification of Cause of Death [Internet]. New Delhi: Office of the Registrar General, India; 2012 [cited 2022 Jun 05]. Available from: ttps://ncdirindia.org/e-mor/Download/Physician's_Manual_MCCD.pdf.

Slik skal dødsmeldingen fylles ut [Internet]. Folkehelseinstituttets; 2019 Sep 13 [updated 2021 Jan 21; cited 2022 Jun 05]. Available from: https://www.fhi.no/hn/helseregistre-og-registre/dodsarsaksregisteret/slik-skal-elektronisk-dodsmelding-fylles-ut/.

US Standard Certificate of Death [Internet]. Atlanta; Centre for Disease Control; revised 2003 Nov (cited 2022 Jun 05]. Available from: https://www.cdc.gov/nchs/data/dvs/DEATH11-03final-ACC.pdf.">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"A65ACBE70014C60DBDB32BFA5FE1B517">
		["build_uid"] = <"7bfeabd4-c4ab-4ac2-b4d4-467cf38c1323">
		["revision"] = <"0.0.1-alpha">
	>

definition
	EVALUATION[at0000] matches {    -- Cause of death
		data matches {
			ITEM_TREE[at0001] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0002] matches {    -- Direct cause of death
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0017] occurrences matches {0..1} matches {    -- Duration
						value matches {
							DV_DURATION matches {
								value matches {|>=PT0S|}
							}
							DV_INTERVAL<DV_DURATION> matches {
								lower matches {
									DV_DURATION matches {
										value matches {|>=PT0S|}
									}
								}
								upper matches {
									DV_DURATION matches {
										value matches {|>=PT0S|}
									}
								}
							}
							DV_TEXT matches {*}
						}
					}
					CLUSTER[at0003] occurrences matches {0..*} matches {    -- Chain of events
						items cardinality matches {1..*; unordered} matches {
							ELEMENT[at0004] matches {    -- Indirect cause
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0005] occurrences matches {0..1} matches {    -- Order
								value matches {
									DV_TEXT matches {*}
									DV_CODED_TEXT matches {
										defining_code matches {
											[local::
											at0006,    -- Contributory cause B
											at0007,    -- Contributory cause C
											at0008]    -- Contributory cause D
										}
									}
								}
							}
							ELEMENT[at0009] occurrences matches {0..1} matches {    -- Timing
								value matches {
									DV_TEXT matches {*}
									DV_INTERVAL<DV_DURATION> matches {
										lower matches {
											DV_DURATION matches {
												value matches {|>=PT0S|}
											}
										}
										upper matches {
											DV_DURATION matches {
												value matches {|>=PT0S|}
											}
										}
									}
									DV_DURATION matches {
										value matches {|>=PT0S|}
									}
									DV_DATE_TIME matches {*}
								}
							}
						}
					}
					allow_archetype CLUSTER[at0012] occurrences matches {0..*} matches {    -- Additional details
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0013] occurrences matches {0..1} matches {    -- Comment
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0018] occurrences matches {0..*} matches {    -- Other condition
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0019] occurrences matches {0..1} matches {    -- Description
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0014] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0015] occurrences matches {0..1} matches {    -- Last updated
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					allow_archetype CLUSTER[at0016] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}

ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Cause of death">
					description = <"Details about the specific disease or condition that caused, or contributed to the death of the individual.">
				>
				["at0001"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0014"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Direct cause of death">
					description = <"Cause leading to death.">
					comment = <"It is strongly recommended that 'Direct cause of death'  be coded with an external terminology. For example: Snomed CT, ICD-10.

This would be cause A in the example:
Cerebral haemorrhage (Direct cause, A)
- due to Metastasis of the brain (B)
- due to Breast cancer (C)">
				>
				["at0003"] = <
					text = <"Chain of events">
					description = <"Causes and/or events including onset date, order and cause diagnosis that led to the individuals death.">
				>
				["at0004"] = <
					text = <"Indirect cause">
					description = <"Name of the condition or event indirectly contributing to death.">
					comment = <"It is strongly recommended that 'Indirect cause' be coded with an external terminology. For example:
Snomed CT, ICD-10.

This would need two instances representing cause B and C in the example:
Cerebral haemorrhage (Direct cause, A)
- due to Metastasis of the brain (B)
- due to Breast cancer (C)">
				>
				["at0005"] = <
					text = <"Order">
					description = <"Sequence of events leading to death, with the most recent first.">
					comment = <"If an alternative valueset is required, these values can be added to the DV_TEXT data type in a template.

As an example:
Cerebral haemorrhage (Direct cause, A) 
- due to Metastasis of the brain (B)
- due to Breast cancer (C)">
				>
				["at0006"] = <
					text = <"Contributory cause B">
					description = <"Due to or as a consequence of.">
				>
				["at0007"] = <
					text = <"Contributory cause C">
					description = <"Due to or as a consequence of.">
				>
				["at0008"] = <
					text = <"Contributory cause D">
					description = <"Due to or as a consequence of.">
				>
				["at0009"] = <
					text = <"Timing">
					description = <"Approximate time from onset of indirect cause to death.">
				>
				["at0015"] = <
					text = <"Last updated">
					description = <"The date cause of death was last updated.">
				>
				["at0013"] = <
					text = <"Comment">
					description = <"Additional narrative about the cause of death not captured in other fields.">
				>
				["at0012"] = <
					text = <"Additional details">
					description = <"Additional details about the cause of death.">
				>
				["at0016"] = <
					text = <"Extension">
					description = <"Additional information required to extend the model with local content or to align with other reference models/formalisms.">
					comment = <"For example: local information requirements; or additional metadata to align with FHIR.">
				>
				["at0017"] = <
					text = <"Duration">
					description = <"Approximate time from onset of direct cause to death.">
				>
				["at0018"] = <
					text = <"Other condition">
					description = <"Other significant conditions contributing to death.">
					comment = <"It is strongly recommended that 'Other condition' be coded with an external terminology. For example: Snomed CT, ICD-10.">
				>
				["at0019"] = <
					text = <"Description">
					description = <"Narrative description about the cause of death including events leading up to death.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Dødsårsak">
					description = <"Detaljer om spesifikk sykdom eller tilstand som forårsaket eller medvirket til individets død.">
				>
				["at0001"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0014"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Umiddelbar dødsårsak">
					description = <"Årsak som førte til død.">
					comment = <"Det anbefales på det sterkeste å kode \"Umiddelbar dødsårsak\" med en ekstern terminologi. For eksempel: Snomed CT eller ICD-10.

Dette vil være årsak A i eksempelet:
Hjerneblødning (Umiddelbar dødsårsak, A)
- som følge av Metastase i hjernen (B)
- som følge av Brystkreft (C)">
				>
				["at0003"] = <
					text = <"Hendelsessekvens">
					description = <"Årsak og / eller hendelser inklusiv dato for sykdom eller hendelse, rekkefølge og årsak som førte til individets død.">
				>
				["at0004"] = <
					text = <"Indirekte dødsårsak">
					description = <"Navn på tilstand eller hendelse som indirekte medvirket til død.">
					comment = <"Det anbefales på det sterkeste å kode \"Indirekte dødsårsak\" med en ekstern terminologi. For eksempel: Snomed CT eller ICD-10.

Her behøves to instanser for å representere årsak B og årsak C i dette eksempelet:
Hjerneblødning (Umiddelbar dødsårsak, A)
- som følge av Metastase i hjernen (B)
- som følge av Brystkreft (C)">
				>
				["at0005"] = <
					text = <"Rekkefølge">
					description = <"Rekkefølge av hendelser, hvor den nyeste hendelsen settes øverst.">
					comment = <"Hvis et alternativt verdisett er nødvendig, kan disse verdiene legges til DV_TEXT-datatypen i et templat.

Som et eksempel:
Hjerneblødning (Umiddelbar dødsårsak, A)
- som følge av Metastase i hjernen (B)
- som følge av Brystkreft (C)">
				>
				["at0006"] = <
					text = <"Medvirkende årsak B">
					description = <"På grunn av eller som følge av.">
				>
				["at0007"] = <
					text = <"Medvirkende årsak C">
					description = <"På grunn av eller som følge av.">
				>
				["at0008"] = <
					text = <"Medvirkende årsak D">
					description = <"På grunn av eller som følge av.">
				>
				["at0009"] = <
					text = <"Varighet indirekte årsak">
					description = <"Omtrentlig tid fra årsaksdebut til død.">
				>
				["at0015"] = <
					text = <"Sist oppdatert">
					description = <"Dato når dødsårsak sist ble oppdatert.">
				>
				["at0013"] = <
					text = <"Kommentar">
					description = <"Ytterligere fritekst om dødsårsak som ikke er fanget opp i andre felt.">
				>
				["at0012"] = <
					text = <"Ytterligere detaljer">
					description = <"Ytterligere detaljer om dødsårsak.">
				>
				["at0016"] = <
					text = <"Tilleggsinformasjon">
					description = <"Ytterligere informasjon som trengs for å kunne registrere lokalt definert innhold eller for å tilpasse til andre referansemodeller/formalismer.">
					comment = <"For eksempel lokale informasjonsbehov eller ytterligere metadata for å kunne tilpasse til tilsvarende konsepter i FHIR.">
				>
				["at0017"] = <
					text = <"Varighet">
					description = <"Omtrentlig tid fra årsaksdebut til død.">
				>
				["at0018"] = <
					text = <"Medvirkende årsak">
					description = <"Andre medvirkende årsaker til død.">
					comment = <"Det anbefales på det sterkeste å kode \"Medvirkende årsak\" med en ekstern terminologi. For eksempel:
Snomed CT eller ICD-10.">
				>
				["at0019"] = <
					text = <"Beskrivelse">
					description = <"Fritekstlig beskrivelse om dødsårsak, inkludert hendelser som førte til dødsfallet.">
				>
			>
		>
	>
