archetype (adl_version=1.4; uid=3e15a5d8-7fee-446d-adb4-783a38772090)
	openEHR-EHR-OBSERVATION.erweiterter_barthel_index.v0

concept
	[at0000]	-- Erweiterter Barthel Index
language
	original_language = <[ISO_639-1::de]>
description
	original_author = <
		["name"] = <"Nina Schewe">
		["organisation"] = <"Medizinische Hochschule Hannover">
		["email"] = <"Schewe.Nina@mh-hannover.de">
		["date"] = <"2023-02-06">
	>
	details = <
		["de"] = <
			language = <[ISO_639-1::de]>
			purpose = <"Der erweiterte Barthel-Index zur Bewertung kognitiver Funktionen einer Person im Alltag anhand einer Punkteskala.

Für jede der 6 zu bewertenden kognitiven Funktionen gibt es eine Tabelle, in der 3 oder 4 Bewertungsmöglichkeiten und deren jeweilige Punktzahl angegeben sind.
Insgesamt können 90 Punkte erreicht werden.">
			use = <"Der Archetyp wird verwendet, um die kognitiven Funktionen einer Person im Alltag anhand einer Punkteskala zu bewerten.">
			keywords = <"Index", "Erweiterter Barthel Index", "Score", "Barthel", "kognitive Funktionen", "Alltägliche Aufgaben">
			misuse = <"Wird nicht verwendet, um den Barthel Index nach dem Hamburger Manual zu erfassen.
Hierfür wird folgender Archetyp verwendet:
openEHR-EHR-OBSERVATION.barthel_index_hamburger_manual.v1.">
			copyright = <"© HiGHmed">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["custodian_organisation"] = <"HiGHmed">
		["references"] = <"DIMDI ICD-10-GM Version 2019 https://www.dimdi.de/static/de/klassifikationen/icd/icd-10-gm/kode-suche/htmlgm2019/zusatz-07-erwbarthelindex.htm">
		["original_namespace"] = <"org.highmed">
		["original_publisher"] = <"HiGHmed">
		["custodian_namespace"] = <"org.highmed">
		["MD5-CAM-1.0.1"] = <"5ACF36A221F19C9A4E5D370927412462">
		["build_uid"] = <"6448c086-56f8-42d2-b815-896de46db257">
		["revision"] = <"0.0.1-alpha">
	>

definition
	OBSERVATION[at0000] matches {    -- Erweiterter Barthel Index
		data matches {
			HISTORY[at0001] matches {    -- Event Series
				events cardinality matches {1..*; unordered} matches {
					EVENT[at0002] occurrences matches {0..1} matches {    -- Jedes Ereignis
						data matches {
							ITEM_TREE[at0003] matches {    -- Baum
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0004] occurrences matches {0..1} matches {    -- Verstehen
										value matches {
											0|[local::at0010],
											5|[local::at0011],
											10|[local::at0012],
											15|[local::at0013]
										}
									}
									ELEMENT[at0005] occurrences matches {0..1} matches {    -- Sich verständlich machen
										value matches {
											0|[local::at0014],
											5|[local::at0015],
											15|[local::at0016]
										}
									}
									ELEMENT[at0006] occurrences matches {0..1} matches {    -- Soziale Interaktion
										value matches {
											0|[local::at0017],
											5|[local::at0018],
											15|[local::at0019]
										}
									}
									ELEMENT[at0007] occurrences matches {0..1} matches {    -- Lösen von Alltagsproblemen
										value matches {
											0|[local::at0020],
											5|[local::at0021],
											15|[local::at0022]
										}
									}
									ELEMENT[at0008] occurrences matches {0..1} matches {    -- Gedächtnis, Lernen und Orientierung
										value matches {
											0|[local::at0023],
											5|[local::at0024],
											10|[local::at0025],
											15|[local::at0026]
										}
									}
									ELEMENT[at0009] occurrences matches {0..1} matches {    -- Sehen/Neglect
										value matches {
											0|[local::at0027],
											5|[local::at0028],
											10|[local::at0029],
											15|[local::at0030]
										}
									}
									ELEMENT[at0031] occurrences matches {0..1} matches {    -- Gesamtpunktzahl
										value matches {
											DV_COUNT matches {*}
										}
									}
									ELEMENT[at0033] occurrences matches {0..1} matches {    -- Auswertung
										value matches {
											DV_TEXT matches {*}
											DV_CODED_TEXT matches {*}
											DV_ORDINAL matches {*}
										}
									}
									ELEMENT[at0032] occurrences matches {0..1} matches {    -- Bemerkungen
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0034] matches {*}    -- Item tree
		}
	}


ontology
	term_definitions = <
		["de"] = <
			items = <
				["at0000"] = <
					text = <"Erweiterter Barthel Index">
					description = <"Der erweiterte Barthel Index dient dem bewerten kognitiver Funktionen anhand einer Punkteskala.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Jedes Ereignis">
					description = <"Standardwert, ein undefinierter/s Zeitpunkt oder Intervallereignis, das explizit im Template oder zur Laufzeit der Anwendung definiert werden kann.">
				>
				["at0003"] = <
					text = <"Baum">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Verstehen">
					description = <"Fähigkeit des Verstehens.">
				>
				["at0005"] = <
					text = <"Sich verständlich machen">
					description = <"Fähigkeit sich verständlich zu machen.">
				>
				["at0006"] = <
					text = <"Soziale Interaktion">
					description = <"Fähigkeit soziale Interaktion durchzuführen.">
				>
				["at0007"] = <
					text = <"Lösen von Alltagsproblemen">
					description = <"Fähigkeit, Alltagsprobleme zu lösen.">
					comment = <"Planung von Handlungsabläufen, Umstellungsfähigkeit, Einhalten von Terminen, pünktliche Medikamenteneinnahme, Einsicht in Defizite und deren Konsequenzen im Alltag.">
				>
				["at0008"] = <
					text = <"Gedächtnis, Lernen und Orientierung">
					description = <"Fähigkeit des Gedächtnisses, des Lernens und der Orientierung.">
				>
				["at0009"] = <
					text = <"Sehen/Neglect">
					description = <"Definieren von Aufmerksamkeitsstörungen.">
				>
				["at0010"] = <
					text = <"Verstehen nicht vorhanden">
					description = <"Einfache Instruktionen oder Fragen werden nicht verstanden.">
				>
				["at0011"] = <
					text = <"Versteht einfache Aufforderungen">
					description = <"Versteht einfache Instruktionen (z.B. „Nehmen Sie bitte die Tablette ein“), in gesprochener
oder geschriebener oder mimischer oder gestischer Form.">
				>
				["at0012"] = <
					text = <"Versteht komplexe Sachverhalte, aber nicht immer">
					description = <"Verständnis nicht immer vorhanden.">
				>
				["at0013"] = <
					text = <"Ungestört">
					description = <"Normales Verstehen (umfasst auch Patienten, die auf Hörhilfen angewiesen sind, nicht jedoch
Patienten, die nur Geschriebenes verstehen).">
				>
				["at0014"] = <
					text = <"Kann sich nicht oder fast nicht verständlich machen">
					description = <"Keine Verständigung.">
				>
				["at0015"] = <
					text = <"Kann einfache Sachverhalte ausdrücken">
					description = <"Kaum verständlich machen.">
				>
				["at0016"] = <
					text = <"Kann sich über fast alles verständlich machen">
					description = <"Kann sich verständlich machen.">
				>
				["at0017"] = <
					text = <"Immer oder fast immer unkooperativ">
					description = <"Unkooperativ.">
				>
				["at0018"] = <
					text = <"Gelegentlich unkooperativ, aggressiv, distanzlos oder zurückgezogen">
					description = <"Gelegentlich unkooperativ.">
				>
				["at0019"] = <
					text = <"Ungestört">
					description = <"Ungestört.">
				>
				["at0020"] = <
					text = <"Benötigt erhebliche Hilfestellung">
					description = <"Hilfestellung notwendig.">
				>
				["at0021"] = <
					text = <"Benötigt geringe Hilfestellung">
					description = <"Geringe Hilfestellung.">
				>
				["at0022"] = <
					text = <"Im wesentlichen ungestört">
					description = <"Ungestört.">
				>
				["at0023"] = <
					text = <"Desorientiert, mit oder ohne Tendenz zum Weglaufen">
					description = <"Desorientiert.">
				>
				["at0024"] = <
					text = <"Muss häufig erinnert werden">
					description = <"Häufiges erinnern.">
				>
				["at0025"] = <
					text = <"Muss gelegentlich erinnert werden oder verwendet externe Gedächtnishilfen">
					description = <"Gelegentliches erinnern notwendig.">
				>
				["at0026"] = <
					text = <"Im wesentlichen ungestört (kein zusätzlicher Pflegeaufwand erforderlich)">
					description = <"Ungestört.">
				>
				["at0027"] = <
					text = <"Findet sich auch in bekannter Umgebung nicht ausreichend zurecht">
					description = <"Starke Aufmerksamkeitsstörung.">
				>
				["at0028"] = <
					text = <"Findet sich in bekannter, aber nicht in unbekannter Umgebung zurecht">
					description = <"Zurechtfinden in bekannter Umgebung.">
				>
				["at0029"] = <
					text = <"Schwere Lesestörung, findet sich aber (ggf. mit Hilfsmitteln) in bekannter und unbekannter Umgebung zurecht">
					description = <"Lesestörung und geringe Aufmerksamkeitsstörung.">
				>
				["at0030"] = <
					text = <"Im wesentlichen ungestört">
					description = <"Ungestört.">
				>
				["at0031"] = <
					text = <"Gesamtpunktzahl">
					description = <"Summe der einzelnen Bewertungen, die für jede der beitragenden Variablen zugewiesen wurden.">
					comment = <"Insgesamt sind unter leichter Reduktion der Abstufungen 90 Punkte erreichbar. ">
				>
				["at0032"] = <
					text = <"Bemerkungen">
					description = <"Bemerkungen, die während des Tests gemacht wurden.">
				>
				["at0033"] = <
					text = <"Auswertung">
					description = <"Hier besteht die Möglichkeit Auswertungen zu definieren, die es zum erweiterten Barthel Index gibt.">
				>
				["at0034"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
			>
		>
	>
