archetype (adl_version=1.4; uid=ead5c940-f3b5-4e7f-8bae-08f5c71b60e2)
	openEHR-EHR-OBSERVATION.diffusionskapazitaet_ergebnis.v0

concept
	[at0000]	-- Diffusionskapazität Ergebnis
language
	original_language = <[ISO_639-1::de]>
description
	original_author = <
		["name"] = <"Alina Rehberg">
		["organisation"] = <"Medizinische Hochschule Hannover">
		["email"] = <" rehberg.alina@mh-hannover.de">
		["date"] = <"2024-10-14">
	>
	details = <
		["de"] = <
			language = <[ISO_639-1::de]>
			purpose = <"Ergebnisse von Diffusionskapazitätsmessungen der Lunge (DLCO-Test) mit Hilfe eines Spirometers oder speziellen Gas-Analysemethoden.">
			use = <"Zur Darstellung der Ergebnisse einer Diffusionskapazitätsmessung. Es handelt sich um eine Lungenfunktionsuntersuchung, die zur Messung des pulmonalen Transferfaktors für Kohlenmonoxid (TLCO bzw. DCO) und der Abschätzung der Diffusionskapazität für Sauerstoff dient.

Das „Jede Ereignis“ kann geklont und eingeschränkt werden, um die Darstellung von Folgendem zu unterstützen:
- Mehrere Messungen und ihr Durchschnitt – klonen Sie „Jedes Ereignis“ so oft wie nötig, plus ein zusätzliches Ereignis, das auf „Intervall“ eingestellt ist und das Attribut „Mittelwert“ auswählt;
- Ergebnisse vor und nach der Bronchialprovokation oder dem Bronchodilatator – klonen Sie „Jedes Ereignis“ und benennen Sie es in „Grundlinie“ und „Postprovokation/Bronchodilatator“ um, bei Bedarf mit einem Zeitversatz verbunden.">
			keywords = <"Diffusionskapazität", "DLCO (Diffusing Capacity of the Lung for Carbon Monoxide)", "TLCO (Transferfaktor der Lunge für Kohlenmonoxid)", "KCO (Kohlenmonoxid-Transferkoeffizient)", "VA (Alveoläres Volumen)", "Gasaustausch", "Kohlenmonoxid-Diffusion", "Transferkoeffizient", "Alveolar-Kapillar-Membran", "Gasaustauschkapazität", "Pulmonale Funktion", "Restriktive Lungenerkrankungen", "Obstruktive Lungenerkrankungen", "Lungenemphysem", "Lungenfibrose", "CO-Aufnahme", "Alveolarmembran", "Hämoglobin-Affinität", "Lungenvolumina", "Ventilation-Perfusion-Verhältnis">
			misuse = <"Nicht zur Aufzeichnung der Ergebnisse anderer Arten von Lungenfunktionstests, beispielsweise Spirometrie oder Bodyplethysmographie, geeignet. Verwenden Sie für diese Zwecke bestimmte Archetypen.

Nicht zur Aufzeichnung der Ergebnisse von Blutgasuntersuchungen geeignet. Verwenden Sie zu diesem Zweck das OBSERVATION.laboratory_test_result.

Nicht zur Aufzeichnung von Messungen zur Pulsoximetrie geeignet. Verwenden Sie zu diesem Zweck die OBSERVATION.pulse_oximetry.">
			copyright = <"© HiGHmed">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["custodian_organisation"] = <"HiGHmed">
		["original_namespace"] = <"org.highmed">
		["original_publisher"] = <"HiGHmed">
		["custodian_namespace"] = <"org.highmed">
		["MD5-CAM-1.0.1"] = <"CCE6F56D400D2E578B1E1A4BB95B8813">
		["build_uid"] = <"1ed56b7c-d267-4dd4-83e5-48d08d3b0ffc">
		["revision"] = <"0.0.1-alpha">
	>

definition
	OBSERVATION[at0000] matches {    -- Diffusionskapazität Ergebnis
		data matches {
			HISTORY[at0001] matches {    -- History
				events cardinality matches {1..*; unordered} matches {
					EVENT[at0002] occurrences matches {0..*} matches {    -- Jedes Ereignis
						data matches {
							ITEM_TREE[at0003] matches {    -- Tree
								items cardinality matches {0..*; unordered} matches {
									CLUSTER[at0007] occurrences matches {0..1} matches {    -- Diffusionskapazität
										items cardinality matches {1..*; unordered} matches {
											ELEMENT[at0006] occurrences matches {0..1} matches {    -- Ergebnis
												value matches {
													C_DV_QUANTITY <
														property = <[openehr::347]>
														list = <
															["1"] = <
																units = <"mmol/(24.h)">
															>
														>
													>
												}
											}
											ELEMENT[at0005] occurrences matches {0..1} matches {    -- Vorhergesagtes Ergebnis
												value matches {
													C_DV_QUANTITY <
														property = <[openehr::347]>
														list = <
															["1"] = <
																units = <"mmol/(24.h)">
															>
														>
													>
												}
											}
											ELEMENT[at0012] occurrences matches {0..1} matches {    -- Gemessenes/vorhergesagtes Verhältnis
												value matches {
													DV_PROPORTION matches {
														numerator matches {|>=0.0|}
														type matches {2}
													}
												}
											}
										}
										name matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0009,    -- TLCO
													at0010,    -- KCO
													at0025]    -- VA
												}
											}
										}
									}
									CLUSTER[at0020] occurrences matches {0..1} matches {    -- Haltezeit
										items cardinality matches {1..*; unordered} matches {
											ELEMENT[at0021] occurrences matches {0..1} matches {    -- Ergebnis
												value matches {
													C_DV_QUANTITY <
														property = <[openehr::128]>
														list = <
															["1"] = <
																units = <"s">
															>
														>
													>
												}
											}
											ELEMENT[at0022] occurrences matches {0..1} matches {    -- Vorhergesagtes Ergebnis
												value matches {
													C_DV_QUANTITY <
														property = <[openehr::128]>
														list = <
															["1"] = <
																units = <"s">
															>
														>
													>
												}
											}
											ELEMENT[at0023] occurrences matches {0..1} matches {    -- Gemessenes/vorhergesagtes Verhältnis
												value matches {
													DV_PROPORTION matches {
														numerator matches {|>=0.0|}
														type matches {2}
													}
												}
											}
										}
										name matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::at0024]    -- t (hold)
												}
											}
										}
									}
									ELEMENT[at0026] occurrences matches {0..1} matches {    -- Klinische Interpretation
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0027] occurrences matches {0..1} matches {    -- Kommentar
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0008] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0028] occurrences matches {0..*} matches {    -- Gerät
						include
							archetype_id/value matches {/.*/}
					}
					allow_archetype CLUSTER[at0029] occurrences matches {0..*} matches {    -- Quelle der vorhergesagten Ergebnisse
						include
							archetype_id/value matches {/.*/}
					}
					allow_archetype CLUSTER[at0030] occurrences matches {0..*} matches {    -- Erweiterung
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["de"] = <
			items = <
				["at0000"] = <
					text = <"Diffusionskapazität Ergebnis">
					description = <"Zur Darstellung der Ergebnisse einer Diffusionskapazitätsmessungen.">
				>
				["at0001"] = <
					text = <"History">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Jedes Ereignis">
					description = <"Jeder Zeitpunkt oder jedes Intervall, das in einem Template oder zur Laufzeit definiert werden kann.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0005"] = <
					text = <"Vorhergesagtes Ergebnis">
					description = <"Vorhergesagte Diffusionskapazität">
				>
				["at0006"] = <
					text = <"Ergebnis">
					description = <"Gemessene Diffusionskapazität">
				>
				["at0007"] = <
					text = <"Diffusionskapazität">
					description = <"Das Testergebnis wird als gemessenes Volumen oder berechnete Kapazität dargestellt.">
					comment = <"Jedes Testergebnis wird über eine separate Instanz dieses Clusters aufgezeichnet.">
				>
				["at0008"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0009"] = <
					text = <"TLCO">
					description = <"Transferfaktor der Lunge für Kohlenmonoxid - Gibt an, wie viel Kohlenmonoxid pro Zeiteinheit von den Alveolen ins Blut übertritt.">
				>
				["at0010"] = <
					text = <"KCO">
					description = <"Kohlenmonoxid-Transferkoeffizient - TLCO normiert auf das Alveolarvolumen (VA), misst die Effizienz des Gasaustauschs pro Volumeneinheit der Lunge.">
				>
				["at0012"] = <
					text = <"Gemessenes/vorhergesagtes Verhältnis">
					description = <"Das Verhältnis der gemessenen zu den vorhergesagten Testergebnissen">
				>
				["at0020"] = <
					text = <"Haltezeit">
					description = <"Das Testergebnis wird als gemessene Haltezeit dargestellt.">
					comment = <"Jedes Testergebnis wird über eine separate Instanz dieses Clusters aufgezeichnet.">
				>
				["at0021"] = <
					text = <"Ergebnis">
					description = <"Gemessene Haltezeit">
				>
				["at0022"] = <
					text = <"Vorhergesagtes Ergebnis">
					description = <"Vorhergesagte Haltezeit">
				>
				["at0023"] = <
					text = <"Gemessenes/vorhergesagtes Verhältnis">
					description = <"Das Verhältnis der gemessenen zu den vorhergesagten Testergebnissen">
				>
				["at0024"] = <
					text = <"t (hold)">
					description = <"Die Haltezeit während eines Atemmanövers in der Lungenfunktionsdiagnostik.">
				>
				["at0025"] = <
					text = <"VA">
					description = <"Alveoläres Volumen - Das Volumen der Lunge, das aktiv am Gasaustausch beteiligt ist.">
				>
				["at0026"] = <
					text = <"Klinische Interpretation">
					description = <"Gesamte klinische Interpretation aller Messungen und berechneten Verhältnisse.">
				>
				["at0027"] = <
					text = <"Kommentar">
					description = <"Zusätzlicher Freitext über die Testergebnisse und deren Interpretation, die nicht in anderen Felder erfasst werden konnte.">
				>
				["at0028"] = <
					text = <"Gerät">
					description = <"Details über das Gerät mit dem die Spirometrie gemessen wird.">
				>
				["at0029"] = <
					text = <"Quelle der vorhergesagten Ergebnisse">
					description = <"Die für die vorhergesagten Ergebnisse verwendete Wissensbasis.">
				>
				["at0030"] = <
					text = <"Erweiterung">
					description = <"Zusätzliche Information, die für die Erfassung des lokalen Kontexts oder für die Anpassung an andere Referenzmodelle/Formalismen benötigt wird">
					comment = <"Zum Beispiel: Informationen bzgl. der lokalen Krankenhausabteilung oder zusätzliche Metadata zur Anpassung an entsprechende FHIR oder CIMI Gegenstücke.">
				>
			>
		>
	>
