archetype (adl_version=1.4; uid=d8f2b17d-adbc-44ba-b458-ba382b3325ed)
	openEHR-EHR-CLUSTER.study_participation.v1

concept
	[at0000]	-- Studienteilnahme
language
	original_language = <[ISO_639-1::de]>
	translations = <
		["en"] = <
			language = <[ISO_639-1::en]>
			author = <
			>
		>
	>
description
	original_author = <
		["name"] = <"Natalia Strauch">
		["organisation"] = <"Medizinische Hochschule Hannover">
		["email"] = <"strauch.natalia@mh-hannover.de">
		["date"] = <"2019-08-01">
	>
	details = <
		["de"] = <
			language = <[ISO_639-1::de]>
			purpose = <"Dieser Archetyp ist dafür bestimmt, die Informationen über eine Teilnahme des Patienten an einer klinischen Prüfung, Beobachtungs-, Register-, Diagnostik-, Therapiestudie oder an einem anderen medizinischen Forschungsvorhaben in der Rolle eines Studienpatienten oder Probanden strukturiert darzustellen.">
			use = <"Zur Darstellung von Informationen über die Teilnahme an einer klinischen Prüfung, Beobachtungs-, Register-, Diagnostik-, Therapiestudie oder an einem anderen medizinischen Forschungsvorhaben.

Dieser Archetyp wurde für die allgemeine Verwendung in anderen Archetypen entwickelt, die Eigenschaften einer Person beschreiben, zum Beispiel DEMOGRAPHIC-PERSON.person-patient oder er kann in Archetypen hinzugefügt werden, die eine Teilnahme an einer Studie detailliert darstellen müssen, wie zum Beispiel in \"Details\" im Archetyp ACTION.informed_consent.">
			keywords = <"Studienteilnahme", "Rekrutierung", "Randomisierung", "Screening", "Studientherapie", "Studienpatient", "Intervention", "Patienteneinwilligung", "Studie", "Klinische Prüfung", "Forschung", "Studienbehandlung", "Studienmedikation", "Proband", "Kohortenstudie", "Beobachtungsstudie", "Studieneinschluss", "Studienaufnahme", "Klinische Studie", "Registerstudie">
			misuse = <"">
			copyright = <"© HiGHmed">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record details about a patient's participation in a clinical trial, observational study, registry study, diagnostic study, therapeutic study, or other medical research project in the role of a study patient or subject in a structured manner.">
			use = <"To record details about participation in a clinical trial, observational, registry, diagnostic, therapeutic study, or other medical research project.

This archetype is designed for general use within archetypes that describe characteristics of a person, for example DEMOGRAPHIC-PERSON.person-patient, or it can be added to archetypes that need to detail participation in a study, such as in \"Details\" in the ACTION.informed_consent archetype.">
			keywords = <"participation", "recruitment", "randomization", "screening", "therapy", "test person", "trial subject", "intervention", "consent", "study", "clinical trial", "research", "treatment", "medication", "cohort study", "observational study", "inclusion", "enrollment", "clinical study", "registry study", "informed", "investigation", "participant">
			misuse = <"">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"published">
	other_contributors = <>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["custodian_organisation"] = <"HiGHmed">
		["references"] = <"ICH-GCP E6 - Guideline for Good Clinical Practice of the International Conference on Harmonisation (CPMP/ICH/135/95);">
		["current_contact"] = <"Natalia Strauch, Medizinische Hochschule Hannover, strauch.natalia@mh-hannover.de">
		["original_namespace"] = <"org.highmed">
		["original_publisher"] = <"HiGHmed">
		["custodian_namespace"] = <"org.highmed">
		["MD5-CAM-1.0.1"] = <"3C9078D449CDD6DEE13D49B93DB8EED2">
		["build_uid"] = <"2515bf61-ca0c-48ce-abd2-937a82134759">
		["revision"] = <"1.0.2">
	>

definition
	CLUSTER[at0000] matches {    -- Studienteilnahme
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0001] occurrences matches {0..1} matches {    -- Name der Studie
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0002] occurrences matches {0..*} matches {    -- Studie
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.study_details(-[a-zA-Z0-9_]+)*\.v0/}
			}
			ELEMENT[at0018] occurrences matches {0..*} matches {    -- Rechtsgrundlage
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0005] occurrences matches {0..1} matches {    -- Status
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0006,    -- Informiert
							at0007,    -- Eingewilligt
							at0008,    -- Screening-Phase
							at0009,    -- Eingeschlossen
							at0010,    -- Widerrufen
							at0011,    -- Abgebrochen/Ausgeschlossen
							at0017,    -- Follow-Up
							at0012]    -- Abgeschlossen
						}
					}
				}
			}
			ELEMENT[at0016] occurrences matches {0..1} matches {    -- Identifikation
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0003] occurrences matches {0..1} matches {    -- Beginn der Teilnahme
				value matches {
					DV_DATE_TIME matches {*}
				}
			}
			ELEMENT[at0004] occurrences matches {0..1} matches {    -- Ende der Teilnahme
				value matches {
					DV_DATE_TIME matches {*}
				}
			}
			allow_archetype CLUSTER[at0015] occurrences matches {0..*} matches {    -- Studienzentrum
				include
					archetype_id/value matches {/.*/}
			}
			ELEMENT[at0014] occurrences matches {0..1} matches {    -- Kommentar
				value matches {
					DV_TEXT matches {*}
				}
			}
		}
	}


ontology
	term_definitions = <
		["de"] = <
			items = <
				["at0000"] = <
					text = <"Studienteilnahme">
					description = <"Detaillierte Informationen über die Teilnahme an einer klinischen Prüfung, Beobachtungs-, Register-, Diagnostik-, Therapiestudie oder an einem anderen medizinischen Forschungsvorhaben in der Rolle eines Studienpatienten oder Probanden.">
				>
				["at0001"] = <
					text = <"Name der Studie">
					description = <"Bezeichnung der Studie bzw. der klinischen Prüfung, an welcher der Patient teilnimmt.">
					comment = <"Der Titel oder Studiencode der Studie, z. B. AIO-HEP-0116.">
				>
				["at0002"] = <
					text = <"Studie">
					description = <"Strukturierte Angaben zur Studie, an welcher der Patient teilnimmt.">
					comment = <"Zum Beispiel:  Studienbeschreibung, Typ der Studie, Sponsor, Studienzentrum, Dauer der Studie usw.">
				>
				["at0003"] = <
					text = <"Beginn der Teilnahme">
					description = <"Datum, wann der Patient in die Studie eingeschlossen wurde.">
					comment = <"Das Datumfeld kann leer bleiben, wenn der Teilnahmestatus auf \"Informiert\" oder \"Eingewilligt\" gesetzt ist.">
				>
				["at0004"] = <
					text = <"Ende der Teilnahme">
					description = <"Datum, wann der Patient die Studie beendet hat.">
					comment = <"Das Datumfeld kann leer bleiben, wenn der Teilnahmestatus auf \"Informiert\", \"Eingewilligt\", \"Screening-Phase\", \"Eingeschlossen\" oder \"Follow-up\" gesetzt ist.">
				>
				["at0005"] = <
					text = <"Status">
					description = <"Status der Teilnahme.">
					comment = <"Ab dem Zeitpunkt, wann ein Patient für eine Studie als Teilnehmer in Frage kommt und darüber informiert wird, kann seine Teilnahme verschiedene Status durchlaufen.">
				>
				["at0006"] = <
					text = <"Informiert">
					description = <"Der Patient wurde über die Studie informiert, aber es wurde noch keine Einwilligung zur Teilnahme erteilt.">
				>
				["at0007"] = <
					text = <"Eingewilligt">
					description = <"Die Einwilligung zur Teilnahme wurde vom Patienten erteilt, aber er wurde in die Studie noch nicht eingeschlossen.">
				>
				["at0008"] = <
					text = <"Screening-Phase">
					description = <"Die Eignungskriterien des Patienten werden derzeit überprüft, bevor eine Intervention stattfindet.">
				>
				["at0009"] = <
					text = <"Eingeschlossen">
					description = <"Der Patient wurde in die Studie eingeschlossen.">
				>
				["at0010"] = <
					text = <"Widerrufen">
					description = <"Der Patient hat seine Einwilligung zurückgezogen, nachdem er bereits eingeschlossen wurde.">
				>
				["at0011"] = <
					text = <"Abgebrochen/Ausgeschlossen">
					description = <"Der Patient hat die Studie aus diversen Gründen von sich aus abgebrochen oder er wurde von Studienverantwortlichen ausgeschlossen.">
				>
				["at0012"] = <
					text = <"Abgeschlossen">
					description = <"Der Patient hat seine Teilnahme regulär beendet.">
				>
				["at0014"] = <
					text = <"Kommentar">
					description = <"Zusätzliche Informationen zu der Studienteilnahme.">
				>
				["at0015"] = <
					text = <"Studienzentrum">
					description = <"Detailangaben über das Studienzentrum, das für den Patienten zuständig ist.">
					comment = <"Zum Beispiel: Name der Einrichtung, Adresse, Name des Prüfers, Kontaktdetails und weitere Details. Hier können Demographische Archetypen eingefügt werden. ">
				>
				["at0016"] = <
					text = <"Identifikation">
					description = <"Die Bezeichnung oder Kennung des Patienten in der Studie.">
					comment = <"Eine eindeutige Bezeichnung des Patienten in der Studie, meist als  Patienten-/Probandennummer oder Patienten ID/Probanden ID genannt.">
				>
				["at0017"] = <
					text = <"Follow-Up">
					description = <"Der Patient befindet sich in der Nachbeobachtungsphase.">
				>
				["at0018"] = <
					text = <"Rechtsgrundlage">
					description = <"Rechtliche Rahmen oder Regeln für die Teilnahme.">
					comment = <"Zur Sicherheit der Patienten dürfen Forschungen am Menschen nur durchgeführt werden, wenn zahlreiche Gesetze und Richtlinien eingehalten werden. Hier können Gesetze und Richtlinien, nach welchen die Teilnahme an der Studie geregelt wird, benannt werden, z.B. das Arzneimittelgesetz (AMG) und die GCP-Verordnung bei einer in Deutschland durchgeführten klinischen Arzneimittelstudie.">
				>
			>
		>
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Study participation">
					description = <"Details about participation in a clinical trial, observational study, registry study, diagnostic study, therapeutic study, or other medical research project in the role of a study patient or subject.">
				>
				["at0001"] = <
					text = <"Study name">
					description = <"Designation of the study or clinical trial in which the patient is participating.">
					comment = <"The title or study code of the study, e.g. AIO-HEP-0116.">
				>
				["at0002"] = <
					text = <"Study">
					description = <"Structured information about the study in which the patient is participating.">
					comment = <"For example: study description, type of study, sponsor, study center, duration of study, etc.">
				>
				["at0003"] = <
					text = <"Start of participation">
					description = <"Date when the patient was enrolled in the study.">
					comment = <"The date field may be left blank if the participation status is set to 'Informed' or 'Consented'.">
				>
				["at0004"] = <
					text = <"End of participation">
					description = <"Date when the patient completed the study.">
					comment = <"The date field can be left blank if the participation status is set to 'Informed', 'Consented', 'Screening Phase', 'Included' or 'Follow-up'.">
				>
				["at0005"] = <
					text = <"Status">
					description = <"Participation status.">
					comment = <"From the time a patient is eligible and informed to be a participant in a study, their participation may go through different statuses.">
				>
				["at0006"] = <
					text = <"Informed">
					description = <"Patient has been informed about the study but has not yet given consent to participate.">
				>
				["at0007"] = <
					text = <"Consented">
					description = <"Consent to participate has been given by the patient, but they have not yet been enrolled in the study.">
				>
				["at0008"] = <
					text = <"Screening phase">
					description = <"The patient's eligibility criteria are currently being reviewed before intervention occurs.">
				>
				["at0009"] = <
					text = <"Included">
					description = <"The patient has been enrolled in the study.">
				>
				["at0010"] = <
					text = <"Revoked">
					description = <"Patient has withdrawn consent after already being enrolled.">
				>
				["at0011"] = <
					text = <"Cancelled/Excluded">
					description = <"The patient has dropped out of the study on their own accord for various reasons or they have been excluded by study officials.">
				>
				["at0012"] = <
					text = <"Completed">
					description = <"The patient has ended his/her participation in a regular manner.">
				>
				["at0014"] = <
					text = <"Comment">
					description = <"Additional information about study participation.">
				>
				["at0015"] = <
					text = <"Study site">
					description = <"Details of the study center responsible for the patient.">
					comment = <"For example: Facility name, address, investigator name, contact details, and other details. Demographic archetypes can be inserted here. ">
				>
				["at0016"] = <
					text = <"Identification">
					description = <"The name or identifier of the patient in the study.">
					comment = <"A unique identifier of the patient in the study, usually called a usually called a Patient/Subject number or Patient ID/Subject ID.">
				>
				["at0017"] = <
					text = <"Follow-up">
					description = <"The patient is in the follow-up phase.">
				>
				["at0018"] = <
					text = <"Legal basis">
					description = <"Legal framework or rules for participation.">
					comment = <"For patient safety, research involving human subjects may only be conducted if numerous laws and guidelines are followed. Laws and guidelines according to which participation in the study is regulated can be named here, e.g. the German Medicinal Products Act (AMG) and the GCP regulation in the case of a clinical drug study conducted in Germany.">
				>
			>
		>
	>
