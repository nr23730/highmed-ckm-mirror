archetype (adl_version=1.4; uid=3d690a80-f80e-4ece-abd0-a7d44d255adc)
	openEHR-EHR-CLUSTER.available_clinical_trials.v1

concept
	[at0000]	-- Studienangebot
language
	original_language = <[ISO_639-1::de]>
description
	original_author = <
		["name"] = <"Natalia Strauch">
		["organisation"] = <"Medizinische Hochschule Hannover">
		["email"] = <"strauch.natalia@mh-hannover.de">
		["date"] = <"2022-01-20">
	>
	details = <
		["de"] = <
			language = <[ISO_639-1::de]>
			purpose = <"Zur Darstellung von Details über ein Angebot einer klinischen Prüfung, Beobachtungs-, Register-, Diagnostik-, Therapiestudie oder eines anderen medizinischen Forschungsvorhabens, das die Patientin / der Patient von dem behandelnden Arzt im Rahmen der Gesundheitsversorgung bekommen kann.">
			use = <"Zur Darstellung von Details über ein Angebot einer klinischen Prüfung, Beobachtungs-, Register-, Diagnostik-, Therapiestudie oder eines anderen medizinischen Forschungsvorhabens, das die Patientin / der Patient von dem behandelnden Arzt im Rahmen der Gesundheitsversorgung bekommen kann.

Verwenden Sie eine Instanz dieses Archetyps, um jedes Studienangebot darzustellen, wenn mehr als eine Studie als Teil einer einzelnen Aktivität als Angebot in Frage kommt.

Der Kontext in dem die Studie angeboten wird, sollte im übergeordneten Archetyp enthalten sein. Beispiel: Details zur klinischen Prüfung, die im Rahmen eines Molekularen Tumorboards empfohlen wird, werden im Archetyp EVALUATION.tumorboard_recommendation dargestellt.">
			keywords = <"Studie", "Intervention", "Klinische Prüfung", "Studienbehandlung", "Studienmedikation", "Umsetzbarkeit", "Feasibility", "Empfehlung", "Machbarkeit", "Therapieangebot", "Tumorboard">
			misuse = <"Der Archetyp darf nicht zur Darstellung von Studien verwendet werden, in der die Person bereits teilnimmt oder zumindest über die Studie informiert wurde. Verwenden Sie zu diesem Zweck den Archetyp CLUSTER.study_participation.">
			copyright = <"© HiGHmed">
		>
	>
	lifecycle_state = <"published">
	other_contributors = <>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["custodian_organisation"] = <"HiGHmed">
		["current_contact"] = <"Natalia Strauch, Medizinische Hochschule Hannover, strauch.natalia@mh-hannover.de">
		["custodian_namespace"] = <"org.highmed">
		["MD5-CAM-1.0.1"] = <"DA332511D5124C60B476DFAF53B187B5">
		["build_uid"] = <"e880c0de-e84b-49e8-8586-bfed6c4cf093">
		["revision"] = <"1.0.0">
	>

definition
	CLUSTER[at0000] matches {	-- Studienangebot
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0001] occurrences matches {0..1} matches {	-- Studie
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0002] occurrences matches {0..1} matches {	-- Studie Details
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.study_details(-[a-zA-Z0-9_]+)*\.v1/}
			}
			ELEMENT[at0003] occurrences matches {0..1} matches {	-- Umsetzbarkeit
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0004, 	-- Gute Umsetzbarkeit des Studienangebots
							at0005, 	-- Mäßige Umsetzbarkeit des Studienangebots
							at0006, 	-- Geringe Umsetzbarkeit des Studienangebots
							at0007]	-- Keine Umsetzbarkeit des Studienangebots
						}
					}
				}
			}
			ELEMENT[at0008] occurrences matches {0..1} matches {	-- Begründung der Umsetzbarkeit
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0009] occurrences matches {0..1} matches {	-- Kommentar
				value matches {
					DV_TEXT matches {*}
				}
			}
		}
	}


ontology
	term_definitions = <
		["de"] = <
			items = <
				["at0000"] = <
					text = <"Studienangebot">
					description = <"Detaillierte Informationen über das Studienangebot für eine Person im Rahmen der Gesundheitsversorgung.">
				>
				["at0001"] = <
					text = <"Studie">
					description = <"Bezeichnung der Studie bzw. der klinischen Prüfung, die angeboten wird.">
					comment = <"Der Titel, Studiencode oder kurze Beschreibung der Studie, z. B. AIO-HEP-0116.">
				>
				["at0002"] = <
					text = <"Studie Details">
					description = <"Strukturierte Angaben zur Studie, die  angeboten werden kann.">
				>
				["at0003"] = <
					text = <"Umsetzbarkeit">
					description = <"Beurteilung der Umsetzbarkeit des Studienangebots für die Person.">
				>
				["at0004"] = <
					text = <"Gute Umsetzbarkeit des Studienangebots">
					description = <"Die Studie kann ohne Aufwand für die Person umgesetzt werden.">
				>
				["at0005"] = <
					text = <"Mäßige Umsetzbarkeit des Studienangebots">
					description = <"Die Studie kann für die Person nur mit viel Aufwand umgesetzt werden.">
				>
				["at0006"] = <
					text = <"Geringe Umsetzbarkeit des Studienangebots">
					description = <"Die Studie kann für die Person kaum umgesetzt werden.">
				>
				["at0007"] = <
					text = <"Keine Umsetzbarkeit des Studienangebots">
					description = <"Die Studie kann für die Person nicht umgesetzt werden.">
				>
				["at0008"] = <
					text = <"Begründung der Umsetzbarkeit">
					description = <"Begründung der Beurteilung zur Umsetzbarkeit des Studienangebots für die Person.">
				>
				["at0009"] = <
					text = <"Kommentar">
					description = <"Ein Kommentar zu dem Studienangebot.">
				>
			>
		>
	>
