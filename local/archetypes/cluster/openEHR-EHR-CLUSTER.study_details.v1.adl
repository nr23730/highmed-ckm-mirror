archetype (adl_version=1.4; uid=716d8eba-5e37-4809-9aec-af4cfe88a45f)
	openEHR-EHR-CLUSTER.study_details.v1

concept
	[at0000]	-- Studie/Prüfung
language
	original_language = <[ISO_639-1::de]>
description
	original_author = <
		["name"] = <"Natalia Strauch">
		["organisation"] = <"Medizinische Hochschule Hannover">
		["email"] = <"strauch.natalia@mh-hannover.de">
		["date"] = <"2019-08-01">
	>
	details = <
		["de"] = <
			language = <[ISO_639-1::de]>
			purpose = <"Dieser Archetyp dient dazu die Informationen über eine klinische Prüfung, Beobachtungs-, Register-, Diagnostik-, Therapiestudie oder ein anderes medizinisches Forschungsvorhaben an Menschen strukturiert darzustellen.">
			use = <"Zur Darstellung von Informationen über eine klinische Prüfung, Beobachtungs-, Register-, Diagnostik-, Therapiestudie oder ein anderes medizinisches Forschungsvorhaben, an welchem eine Person teilnimmt oder teilgenommen hat.

Unter \"medizinisches Forschungsvorhaben an Menschen\" ist ein Vorhaben zu verstehen, das ethischen Standards unterliegt und somit ein Ethikvotum erfordert. Das setzt voraus, dass von einem Patienten, der an so einem Forschungsvorhaben teilnimmt, eine freiwillige informierte Einwilligung (Einwilligung nach Aufklärung ‐„informed consent“)  in schriftlicher Form, vor dem Beginn der Teilnahme, vorliegen muss.

Zwischen den Begriffen \"Studie\" und \"Prüfung\" gibt es im Allgemeinen und in vielen medizinischen Fachkreisen keine klare Grenze und im klinischen Bereich werden diese oft als Synonyme betrachtet. Eine klare Definition gibt es nicht. Nichtdestotrotz, können diese Begriffe je nach Kontext unterschiedlich interpretiert werden. Um allen Fachkreisen und jedem Gebrauch des Archetyps gerecht zu werden, werden beide Begriffe im Konzept dieses Archetyps verwendet.

Dieser Archetyp wurde für die allgemeine Verwendung in anderen Archetypen entwickelt, die ein medizinisches Forschungsvorhaben an einem Patienten detailliert beschreiben müssen. Beispiele sind: ACTION.informed_consent für die Studiendetails; CLUSTER.study_participation, um detailliert die Studie, an welcher der Patient teilnimmt, zu beschreiben.">
			keywords = <"Klinische Prüfung", "Studie", "Forschung", "Forschungsvorhaben", "Forschungsstudie", "klinischer Test", "Beobachtungsstudie", "Anwendungsbeobachtung", "Registerstudie", "Diagnostikstudie", "Therapiestudie", "Observation", "Kohortenstudie", "Arzneimittelstudie", "Medizinproduktenstudie", "Arzneimittelprüfung", "Medikamentenprüfung", "Randomisation", "Proband", "Placebo", "Fallstudie", "Verblindet", "Verum", "Kontrollgruppe", "Studienrekrutierung", "Screening", "Patienteneinwilligung", "Informed Consent", "Studienpatient", "Ethikkommission", "Prüfplan", "Studienprotokoll", "Prüfer", "Sponsor", "Intervention", "Studie Phase", "Klinische Studie", "Registerstudie">
			misuse = <"Nicht zur Darstellung von nicht medizinischen Teststudien mit Nahrungsergänzungsmitteln oder anderen Konsumprodukten, wie z. B. Test eines Produktes zum Abnehmen oder eines kosmetischen Mittels.

Nicht zur Darstellung von anonymisierten Studien.">
			copyright = <"© HiGHmed">
		>
	>
	lifecycle_state = <"published">
	other_contributors = <>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["custodian_organisation"] = <"HiGHmed">
		["references"] = <"ICH-GCP E6 - Guideline for Good Clinical Practice of the International Conference on Harmonisation (CPMP/ICH/135/95);

https://clinicaltrials.gov

http://www.ema.europa.eu/docs/en_GB/document_library/Scientific_guideline/2009/09/WC500002874.pdf
https://www.clinicaltrialsregister.eu">
		["current_contact"] = <"Natalia Strauch, Medizinische Hochschule Hannover, strauch.natalia@mh-hannover.de">
		["original_namespace"] = <"org.highmed">
		["original_publisher"] = <"HiGHmed">
		["custodian_namespace"] = <"org.highmed">
		["MD5-CAM-1.0.1"] = <"4244C5EC7B82DFE4829DD920989058F8">
		["build_uid"] = <"e8e2185a-2f7d-473c-bab4-5edad4c0c628">
		["revision"] = <"1.0.0">
	>

definition
	CLUSTER[at0000] matches {	-- Studie/Prüfung
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0001] matches {	-- Titel der Studie/Prüfung
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0004] occurrences matches {0..1} matches {	-- Beschreibung
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0002] occurrences matches {0..1} matches {	-- Studien Code
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0003] occurrences matches {0..1} matches {	-- Studientyp
				value matches {
					DV_TEXT matches {*}
				}
			}
			CLUSTER[at0033] occurrences matches {0..*} matches {	-- Registrierung
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0035] occurrences matches {0..1} matches {	-- Registername
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0034] occurrences matches {0..1} matches {	-- Registrierungsnummer
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
			ELEMENT[at0012] occurrences matches {0..1} matches {	-- Sponsor
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0013] occurrences matches {0..1} matches {	-- Studienleiter
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0023] occurrences matches {0..*} matches {	-- Studienzentrum
				include
					archetype_id/value matches {/.*/}
			}
			ELEMENT[at0010] occurrences matches {0..1} matches {	-- Beginn der Studie
				value matches {
					DV_DATE matches {*}
				}
			}
			ELEMENT[at0015] occurrences matches {0..1} matches {	-- Ende der Studie
				value matches {
					DV_DATE matches {*}
				}
			}
			ELEMENT[at0016] occurrences matches {0..1} matches {	-- Phase 0
				value matches {
					DV_BOOLEAN matches {
						value matches {True, False; False}
					}
				}
			}
			ELEMENT[at0017] occurrences matches {0..1} matches {	-- Phase I
				value matches {
					DV_BOOLEAN matches {
						value matches {True, False; False}
					}
				}
			}
			ELEMENT[at0018] occurrences matches {0..1} matches {	-- Phase II
				value matches {
					DV_BOOLEAN matches {
						value matches {True, False; False}
					}
				}
			}
			ELEMENT[at0019] occurrences matches {0..1} matches {	-- Phase III
				value matches {
					DV_BOOLEAN matches {
						value matches {True, False; False}
					}
				}
			}
			ELEMENT[at0020] occurrences matches {0..1} matches {	-- Phase IV
				value matches {
					DV_BOOLEAN matches {
						value matches {True, False; False}
					}
				}
			}
			ELEMENT[at0005] occurrences matches {0..1} matches {	-- Status
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0024, 	-- In Vorbereitung
							at0025, 	-- Rekrutierung
							at0026, 	-- Einladung zum Screening
							at0027, 	-- Aktiv, keine Rekrutierung
							at0028, 	-- Vorübergehend unterbrochen
							at0029, 	-- Abgeschlossen
							at0030, 	-- Vorzeitiges Ende
							at0031, 	-- Widerrufen
							at0032]	-- Unbekannt
						}
					}
				}
			}
			allow_archetype CLUSTER[at0014] occurrences matches {0..*} matches {	-- Zusätzliche Details
				include
					archetype_id/value matches {/.*/}
			}
		}
	}


ontology
	term_definitions = <
		["de"] = <
			items = <
				["at0000"] = <
					text = <"Studie/Prüfung">
					description = <"Detaillierte Informationen über eine klinische Prüfung, Beobachtungs-, Register-, Diagnostik-, Therapiestudie oder ein anderes medizinisches Forschungsvorhaben an Menschen.">
				>
				["at0001"] = <
					text = <"Titel der Studie/Prüfung">
					description = <"Titel des Forschungsvorhabens.">
					comment = <"Zum Beispiel: \"Eine randomisierte Phase-II-Studie mit nal-Iri plus 5-Fluorouracil im Vergleich zu 5-Fluorouracil bei stationären Patienten mit Cholangio- und Gallenblasenkarzinom, die zuvor mit Gemcitabin oder Gemcitabin-haltigen Therapien behandelt wurden.\"">
				>
				["at0002"] = <
					text = <"Studien Code">
					description = <"Die eindeutige Bezeichnung der Studie, welche der Sponsor (Studienverantwortliche) vergeben hat.">
					comment = <"Jedes Studienvorhaben wird nach einem Prüfplan (Studienprotokoll) durchgeführt, der durch den Verantwortlichen der Studie eindeutig bezeichnet wird, z. B. AIO-HEP-0116.">
				>
				["at0003"] = <
					text = <"Studientyp">
					description = <"Beschreibt die Art einer Studie.">
					comment = <"Zum Beispiel: Interventionelle klinische Prüfung, Observationsstudie, Registerstudie, Bioäquivalenz-Studie, Medizinproduktestudie oder andere.">
				>
				["at0004"] = <
					text = <"Beschreibung">
					description = <"Kurze Beschreibung des Forschungsvorhabens.">
					comment = <"Beschreibung des Forschungsvorhabens in leicht verständlicher Formulierung für Laien.">
				>
				["at0005"] = <
					text = <"Status">
					description = <"Status der Studie.">
					comment = <"Der Stand der Studie im zeitlichen Prozessverlauf.">
				>
				["at0010"] = <
					text = <"Beginn der Studie">
					description = <"Genaues oder geschätztes Datum, wann die Studie beginnt oder begonnen hat.">
					comment = <"Das Datum, an dem der erste Teilnehmer in die Studie eingeschlossen wird.">
				>
				["at0012"] = <
					text = <"Sponsor">
					description = <"Name des Sponsors.">
					comment = <"Angaben über eine natürliche oder juristische Person, die die Verantwortung für die Veranlassung, Organisation und Finanzierung eines Forschungsvorhabens bei Menschen übernimmt.
 Zum Beispiel: Novartis AG.">
				>
				["at0013"] = <
					text = <"Studienleiter">
					description = <"Name des ärztlichen Leiters des Studienvorhabens.">
					comment = <"Name einer Person, die die ärztliche Leitung der Studie übernimmt.">
				>
				["at0014"] = <
					text = <"Zusätzliche Details">
					description = <"Zusätzliche strukturierte Angaben zur Studie.">
					comment = <"Zum Beispiel detaillierte Angaben über die Genehmigungsbehörde, zum Studiendesign oder -plan, zum Sponsor, zu den Therapiearmen und/oder zum Studienmedikament oder Ein-/Ausschlusskriterien als Voraussetzung für eine Studienteilnahme.">
				>
				["at0015"] = <
					text = <"Ende der Studie">
					description = <"Genaues oder geschätztes Datum für das Ende des Studienvorhabens.">
					comment = <"Das Datum, an dem der zuletzt verbleibende Teilnehmer die Studie beendet hat.">
				>
				["at0016"] = <
					text = <"Phase 0">
					description = <"Angabe, ob es sich um eine klinische Prüfung der Phase 0 handelt.">
					comment = <"Eine Forschungsphase, die vor traditionellen Phase-I-Studien durchgeführt wird, um zu untersuchen, wie oder ob ein Medikament den menschlichen Körper beeinflusst. Explorative Studie ohne therapeutische oder diagnostische Ziele (z. B. Screening-Studie, Mikrodosis-Studie).">
				>
				["at0017"] = <
					text = <"Phase I">
					description = <"Angabe, ob es sich um eine klinische Prüfung der Phase I handelt.">
					comment = <"Eine Forschungsphase, die sich mit der Sicherheit eines Arzneimittels befasst. Die Studie wird in der Regel mit gesunden Freiwilligen durchgeführt.">
				>
				["at0018"] = <
					text = <"Phase II">
					description = <"Angabe, ob es sich um eine klinische Prüfung der Phase II handelt.">
					comment = <"In dieser Phase wird vor allem untersucht, ob das Prüfpräparat wirksam und verträglich ist. Außerdem dient diese Phase dazu, die richtige Dosis zu finden.">
				>
				["at0019"] = <
					text = <"Phase III">
					description = <"Angabe, ob es sich um eine klinische Prüfung der Phase III handelt.">
					comment = <"Eine Forschungsphase, in der mehr Informationen über die Sicherheit und Wirksamkeit eines Arzneimittels gesammelt werden, indem verschiedene Populationen und unterschiedliche Dosierungen untersucht werden und das Arzneimittel in Kombination mit anderen Arzneimitteln angewendet wird.">
				>
				["at0020"] = <
					text = <"Phase IV">
					description = <"Angabe, ob es sich um eine klinische Prüfung der Phase IV handelt.">
					comment = <"Eine Phase der Forschung mit einem bereits zugelassenem Medikament.  Dazu gehört z. B. eine Post-Marketing-Studie. In dieser Studie werden zusätzliche Informationen zur Sicherheit, Wirksamkeit oder optimalen Verwendung eines Arzneimittels gesammelt.">
				>
				["at0023"] = <
					text = <"Studienzentrum">
					description = <"Detailangaben über die teilnehmende medizinische Einrichtung wie Klinik oder Praxis, die Patienten gemäß den Studienvorgaben rekrutiert und die Daten erhebt.">
					comment = <"Zum Beispiel: Name der Einrichtung, Adresse, Name des Prüfers, Kontaktdetails, zuständige Ethikkommission, Datum der Genehmigung der Teilnahme, beteiligte Personen und weitere Details. Hier können auch demographische Archetypen eingefügt werden. ">
				>
				["at0024"] = <
					text = <"In Vorbereitung">
					description = <"Es wurde mit der Rekrutierung der Teilnehmer noch nicht begonnen.">
				>
				["at0025"] = <
					text = <"Rekrutierung">
					description = <"Derzeit werden Teilnehmer für die Studie rekrutiert.">
				>
				["at0026"] = <
					text = <"Einladung zum Screening">
					description = <"Die künftigen Teilnehmer für die Studie werden derzeit aus einer Population oder einer Gruppe von Personen ausgewählt und zur Überprüfung der Eignungskriterien eingeladen.">
				>
				["at0027"] = <
					text = <"Aktiv, keine Rekrutierung">
					description = <"Die Studie läuft, aber potenzielle Teilnehmer werden derzeit nicht rekrutiert oder eingeschrieben.">
				>
				["at0028"] = <
					text = <"Vorübergehend unterbrochen">
					description = <"Die Studie wurde vorzeitig gestoppt, kann jedoch erneut gestartet werden.">
				>
				["at0029"] = <
					text = <"Abgeschlossen">
					description = <"Die Studie wurde regulär beendet.">
				>
				["at0030"] = <
					text = <"Vorzeitiges Ende">
					description = <"Die Studie wurde vorzeitig beendet und wird nicht erneut gestartet. Teilnehmer werden nicht mehr untersucht oder behandelt.">
				>
				["at0031"] = <
					text = <"Widerrufen">
					description = <"Die Studie wurde vorzeitig abgebrochen, bevor der erste Teilnehmer eingeschlossen wurde.">
				>
				["at0032"] = <
					text = <"Unbekannt">
					description = <"Der Status der Studie kann nicht ermittelt werden.">
				>
				["at0033"] = <
					text = <"Registrierung">
					description = <"Registrierung der Studie in Registern.">
					comment = <"Wenn die Studie auf der Webseite Clinicaltrials.gov registriert ist, besitzt sie eine US NCT-Nummer. Zum Beispiel: NCT03772405. 
Eine EudraCT Nummer wird von der Europäischen Arzneimittelagentur vergeben. Wenn die klinische Prüfung auf der Webseite Current Controlled Trials registriert ist, besitzt sie eine ISRCTN-Nummer (International Standard Randomised Controlled Trial Number). ">
				>
				["at0034"] = <
					text = <"Registrierungsnummer">
					description = <"Eindeutige Identifikationsnummer an dem angezeigten Register.">
					comment = <"Zum Beispiel die EudraCT Nummer, die von der Europäischen Arzneimittelagentur vergeben wird, oder ISRCTN (International Standard Randomised Controlled Trial Number). 
Wenn die klinische Prüfung auf der Webseite Current Controlled Trials registriert ist, besitzt sie eine ISRCTN-Nummer.">
				>
				["at0035"] = <
					text = <"Registername">
					description = <"Studienregister, wo die Studie registriert ist und eine eindeutige Identifikationsnummer besitzt.">
					comment = <"Zum Beispiel: Europäischen Arzneimittelagentur (EudraCT) oder Webseite Clinicaltrials.gov (US NCT-Nummer).">
				>
			>
		>
	>
